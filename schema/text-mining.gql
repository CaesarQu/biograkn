define

# common attributes
confidence sub attribute, datatype double;

pubmed-article sub entity,
  has pmid,
  has title,
  has annotated-abstract;

pmid sub attribute, datatype string;
title sub attribute, datatype string;
annotated-abstract sub attribute, datatype string,
  plays abstract-container,
  plays owner;

sentence sub entity,
  has sentiment,
  has text,
  plays contained-sentence,
  plays sentence-container,
  plays mentioning-sentence;

sentiment sub attribute, datatype string;
text sub attribute, datatype string;

token sub entity,
  has start-position,
  has end-position,
  has tag,
  has ner,
  has lemma,
  plays contained-token,
  plays object,
  plays subject;

start-position sub attribute, datatype double;
end-position sub attribute, datatype double;
tag sub attribute, datatype string;
ner sub attribute, datatype string,
    has confidence;
lemma sub attribute, datatype string;

mined-relation sub relation,
    has relation-type,
    has confidence,
    relates object,
    relates subject,
    relates mentioning-sentence;

relation-type sub attribute, datatype string;

entity-mention sub entity,
    has value,
    has entity-type,
    plays mentioned-entity,
    plays containing-entity;

entity-type sub attribute, datatype string;
value sub attribute, datatype string;

mention sub relation,
    relates mentioned-entity,
    relates mentioning-sentence,
    plays owned,
    plays representative;

coref-chain sub relation,
    has chain-id,
    relates owner,
    relates owned,
    relates representative;

chain-id sub attribute, datatype double;


abstract-containing-sentence sub relation,
    relates abstract-container,
    relates contained-sentence;

sentence-containing-token sub relation,
    relates sentence-container,
    relates contained-token;

entity-containing-token sub relation,
    relates containing-entity,
    relates contained-token;

test1 sub rule,
when {
    (representative: $r, owned: $o) isa coref-chain;
    $o (mentioned-entity: $e) isa mention;
    (containing-entity: $e, contained-token: $t) isa entity-containing-token;
    (subject: $t, mentioning-sentence: $s, object: $ob) isa mined-relation, has confidence $c, has relation-type $rt;

    $r (mentioned-entity: $e2) isa mention;
    ($e2, $t2) isa entity-containing-token;
},
then {
    (subject: $t2, mentioning-sentence: $s, object: $ob) isa mined-relation;
};

test2 sub rule,
  when {
      (representative: $r, owned: $o) isa coref-chain;
      $o (mentioned-entity: $e) isa mention;
      (containing-entity: $e, contained-token: $t) isa entity-containing-token;
      (subject: $t, mentioning-sentence: $s, object: $ob) isa mined-relation, has confidence $c, has relation-type $rt;

      $r (mentioned-entity: $e2) isa mention;
      ($e2, $t2) isa entity-containing-token;
      $mr2 (subject: $t2, mentioning-sentence: $s, object: $ob) isa mined-relation;
  },
  then {
      $mr2 has confidence $c;
  };

 test3 sub rule,
 when {
     (representative: $r, owned: $o) isa coref-chain;
     $o (mentioned-entity: $e) isa mention;
     (containing-entity: $e, contained-token: $t) isa entity-containing-token;
     (subject: $t, mentioning-sentence: $s, object: $ob) isa mined-relation, has confidence $c, has relation-type $rt;

     $r (mentioned-entity: $e2) isa mention;
     ($e2, $t2) isa entity-containing-token;
     $mr2 (subject: $t2, mentioning-sentence: $s, object: $ob) isa mined-relation;
 },
 then {
     $mr2 has relation-type $rt;
 };